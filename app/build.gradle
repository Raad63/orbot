import org.jetbrains.kotlin.gradle.dsl.JvmTarget
import org.jetbrains.kotlin.gradle.tasks.KotlinJvmCompile

plugins {
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.kotlin.serialization)
    alias(libs.plugins.android.application)
}

// Gets the version name from the latest Git tag, stripping the leading v off
def getVersionName = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'describe', '--tags', '--always'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}


def keystorePropertiesFile = rootProject.file("keystore.properties")
def keystoreProperties = new Properties()
if (keystorePropertiesFile.canRead()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

// Increments versionCode by ABI type
def abiCodeMap = ["armeabi-v7a": "1", "arm64-v8a": "2", "x86": "4", "x86_64": "5"]
android.applicationVariants.all { variant ->
    variant.outputs.each { output ->
        def baseVersionCode = abiCodeMap.get(output.getFilter("ABI"))
        if (baseVersionCode != null) {
            output.versionCodeOverride = Integer.valueOf(variant.versionCode) + Integer.valueOf(baseVersionCode)
        }
    }
}

android {
    namespace = "org.torproject.android"
    compileSdk = 36

    defaultConfig {
        applicationId "org.torproject.android"
        versionName getVersionName()
        minSdk = 24
        targetSdk = 35
        multiDexEnabled true
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        archivesBaseName = "Orbot"
    }


    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    signingConfigs {
        release {
            if (!keystoreProperties.stringPropertyNames().isEmpty()) {
                keyAlias keystoreProperties["keyAlias"]
                keyPassword keystoreProperties["keyPassword"]
                storeFile file(keystoreProperties["storeFile"])
                storePassword keystoreProperties["storePassword"]
            }
        }
    }

    buildTypes {
        release {
            proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.txt"
            signingConfig signingConfigs.release
        }
    }

    buildFeatures {
        buildConfig true
        viewBinding true
    }

    buildTypes {
        release {
            shrinkResources false
            minifyEnabled false
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
    }

    splits {
        abi {
            enable true
            reset()
            include "x86", "armeabi-v7a", "x86_64", "arm64-v8a"
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_21
        targetCompatibility = JavaVersion.VERSION_21
    }


    flavorDimensions = ["free"]

    productFlavors {
        fullperm {
            dimension "free"
            applicationId "org.torproject.android"
            versionCode 1750300100
            versionName getVersionName()
            archivesBaseName = "Orbot-${versionName}"
        }

        nightly {
            dimension "free"
            applicationId "org.torproject.android.nightly"
            versionName getVersionName()
            versionCode versionCodeEpoch()
            archivesBaseName = "Orbot-${versionName}"
        }
    }

    splits {
        abi {
            universalApk true
        }
    }

    packagingOptions {
        resources {
            excludes += ['META-INF/androidx.localbroadcastmanager_localbroadcastmanager.version']
        }
    }

    lint {
        abortOnError false
        checkReleaseBuilds false
        disable 'InvalidPackage'
        htmlReport true
        lintConfig file('../lint.xml')
        textReport false
        xmlReport false
    }
}


private static def versionCodeEpoch() {
    return (new Date().getTime() / 1000).toInteger()
}

dependencies {
    // Use locally built ipt_proxy+go_tun2socks
    implementation(project(":OrbotLib"))
    implementation(project(":orbotservice"))

    implementation(libs.android.material)
    implementation(libs.android.volley)
    implementation(libs.androidx.activity)
    implementation(libs.androidx.core.ktx)
    implementation(libs.androidx.localbroadcast)
    implementation(libs.androidx.navigation.fragment.ktx)
    implementation(libs.androidx.navigation.ui.ktx)
    implementation(libs.retrofit.converter)
    implementation(libs.retrofit.lib)
    implementation(libs.rootbeer.lib)
    implementation(libs.kotlinx.coroutines.core)
    implementation(libs.kotlinx.serialization.json)
    implementation(libs.appiconnamechanger)
    implementation(libs.androidx.preference)
    implementation(libs.androidx.appcompat)
    implementation(libs.androidx.biometric)
    implementation(libs.androidx.lifecycle.common)
    implementation(libs.androidx.lifecycle.process)

    testImplementation(libs.junit.jupiter)
    androidTestImplementation(libs.androidx.junit)
    androidTestImplementation(libs.androidx.espresso)
    androidTestImplementation(libs.androidx.rules)
    androidTestImplementation(libs.androidx.runner)
    androidTestImplementation(libs.sreengrab)
    androidTestUtil libs.androidx.orchestrator

}

tasks.register("copyLicenseToAssets", Copy) {
    from("../LICENSE")
    into("$projectDir/src/main/assets")
}

tasks.withType(KotlinJvmCompile).configureEach {
    compilerOptions {
        jvmTarget.set(JvmTarget.JVM_21)
    }
}

preBuild.dependsOn copyLicenseToAssets
